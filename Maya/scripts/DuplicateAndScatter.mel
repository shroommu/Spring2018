global proc DupAndScatter (int $dupNum, float $rangeMinX, float $rangeMaxX, float $rangeMinY, float $rangeMaxY, float $rangeMinZ, float $rangeMaxZ){
    
    //store selected object
    string $sels[] = `ls -selection`;
    
    //duplicate and move selected object according to min/max and dupNum vars
    for ($i = 0; $i < $dupNum; $i++)
    {
        //duplicate and assign to $sels
        duplicate -rr $sels;
        $sels = `ls -selection`;
        print ($sels);
        
        //generate random point
        float $tempX = rand ($rangeMinX, $rangeMaxX);
        float $tempY = rand ($rangeMinY, $rangeMaxY);
        float $tempZ = rand ($rangeMinZ, $rangeMaxZ);
        
        string $sel = $sels[0];
        
        //move to world 0
        setAttr ($sel + ".translateX") 0;
        setAttr ($sel + ".translateY") 0;
        setAttr ($sel + ".translateZ") 0;
        
        //move to random point
        move -r -worldSpace $tempX $tempY $tempZ;
    }
}

global proc float QueryValue(string $objName){
    float $value = `floatField -q -v $objName`;
    return $value;
}

{
    global string $numDups;
    global string $minXField;
    global string $maxXField;
    global string $minYField;
    global string $maxYField;
    global string $minZField;
    global string $maxZField;   
    
	string $window = `window -widthHeight 200 250`;
	frameLayout;
	gridLayout -numberOfColumns 2 -cellWidthHeight 150 20;
	    text -label "Duplicate Number";
		$numDups = `floatField -value 1`;
        text -label "Min Range X";
		$minXField = `floatField -value -10`;
		text -label "Max Range X";
		$maxXField = `floatField -value 10`;
		text -label "Min Range Y";
		$minYField = `floatField -value -10`;
		text -label "Max Range Y";
		$maxYField = `floatField -value 10`;
		text -label "Min Range Z";
		$minZField = `floatField -value -10`;
		text -label "Max Range Z";
		$maxZField = `floatField -value 10`;
		button -label "Duplicate and scatter" -command "DupAndScatter(`QueryValue($numDups)`, `QueryValue($minXField)`, `QueryValue($maxXField)`, `QueryValue($minYField)`, `QueryValue($maxYField)`, `QueryValue($minZField)`, `QueryValue($maxZField)`);";
	showWindow $window;
}